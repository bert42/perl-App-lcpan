#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any -prefer_lite=>1;

my $prefix = '/CPAN/Local/';

Perinci::CmdLine::Any->new(
    url => $prefix,
    log => 1,
    subcommands => {
        update          => {url=>"${prefix}update_local_cpan"},
        'update-index'  => {url=>"${prefix}update_local_cpan_index"},
        'update-files'  => {url=>"${prefix}update_local_cpan_files"},

        stats      => {url=>"${prefix}stat_local_cpan"},

        authors    => {url=>"${prefix}list_local_cpan_authors"},
        modules    => {url=>"${prefix}list_local_cpan_modules"},
        dists      => {url=>"${prefix}list_local_cpan_dists"},
        deps       => {url=>"${prefix}list_local_cpan_deps"},
        'rev-deps' => {url=>"${prefix}list_local_cpan_rev_deps"},
        #search => {url=>""},
    },
)->run;

# ABSTRACT: Manage your local CPAN mirror
# PODNAME:

=head1 SYNOPSIS

=head2 Update (download and index) your local CPAN mirror

 % lcpan update

A mini CPAN mirror will be downloaded in your C<~/cpan> directory. This will
take a while (it downloads +- 4GB of files, as of this writing; subsequent
update will of course take shorter time.)

A SQLite database is also created/updated in C<~/cpan/index.db> which contains
information about authors, modules/packages, distributions. This database is
used for quickly answer various queries.

You can run the above command e.g. daily to keep your mirror up-to-date.

=head2 Using your local CPAN mirror to install modules

 % cpanm --mirror ~/cpan --mirror-only -n Some::Module

You can also set up a shell alias to save typing the options all the time, e.g.:

 alias lcpanm="cpanm --mirror ~/cpan --mirror-only -n"

Then you can install modules simply using:

 % lcpanm Some::Module

=head2 Querying your local CPAN mirror

Info about your local CPAN mirror:

 % lcpan stats
 +------------------+----------------------+
 | name             | value                |
 +------------------+----------------------+
 | authors          | 11967                |
 | distributions    | 29907                |
 | last-updated     | 2015-01-09T10:40:55Z |
 | modules          | 13365                |
 | packages         | 133650               |
 +------------------+----------------------+

Add C<--verbose> if you want more stats which normally are skipped because they
can take a while to get (e.g. disk-space).

B<List modules:>

 % lcpan modules

List modules matching some query (name):

 % lcpan modules smtp

List modules by certain author, show detail instead of just module names:

 % lcpan modules --author PERLANCAR --detail

List modules from a certain dist:

 % lcpan modules --dist libwww-perl --detail

B<List distributions:>

 % lcpan dists

List distributions matching some query (name, filename, abstract), show detail
instead of just distribution name:

 % lcpan dists critic --detail

List distributions released by a certain author:

 % lcpan dists --author PERLANCAR

B<List authors:>

 % lcpan authors

List authors matching some query (PAUSE ID, name, email), show detail instead of
just ID:

 % lcpan authors tatsuhiko --detail

B<Show dependencies and reverse dependencies>:

 # which modules does Text::ANSITable depend on?
 % lcpan deps Text::ANSITable

 # do it recursively
 % lcpan deps Text::ANSITable -R

 # which distributions depend on Text::ANSITable?
 % lcpan rev-deps Text::ANSITable


=head1 DESCRIPTION

This application is a convenient bundling of C<CPAN::Mini> and an indexer so in
addition to creating your local CPAN mirror and installing modules from it, you
can also query various things about your local CPAN mirror quickly from the
command-line (as well as programmatically).


=head1 FAQ


=head1 SEE ALSO

